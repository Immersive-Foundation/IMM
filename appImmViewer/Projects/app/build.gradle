apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'

android {
    signingConfigs {
        release {
//            storeFile file('C:/Users/schevrel/Dropbox (Facebook)/Quill/store/Quest/quill-player-beta-061019.jks')
//            storeFile file('/Users/harleym/Dropbox (Facebook)/Quill Project/quill-player-beta-061019.jks')
//            storeFile file('/Users/plafayette/Dropbox (Facebook)/quill-art-samples/gallery-6-options/store-assets/quill-player-beta-061019.jks')
//            storeFile file('Z:\\quill-player-beta-061019.jks')
            storePassword 'quilldev'
            keyPassword 'quilldev'
            keyAlias = 'quillplayer-beta'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    compileSdkVersion 30
    ndkVersion "20.1.5948944"

    defaultConfig {
        applicationId "com.facebook.arvr.demoquillplayer"
        minSdkVersion 25
        targetSdkVersion 29
        // Keep these in sync with .../exeQuillPlayer/BUCK
        versionCode 98
        versionName "1.6.2 03-19-2021 (D27210257)"

        externalNativeBuild {
            cmake {
                //"-DLOCALIZED_TEXT", // add to cppFlags to enable new localized text rendering
                cppFlags "-std=c++17", "-DANDROID", "-Werror", "-Wno-inconsistent-missing-override",  "-DUSE_28_BYTE_VERTS=1", "-DST_USE_32_BYTE_VERTS=1", "-DPT_USE_20_BYTE_VERTS=1", "-DBATCH_UI=1", "-DNEW_RENDERING=1", "-DUNLOAD_SOUNDS=1"
                arguments "-DANDROID_STL=c++_shared"
                abiFilters "arm64-v8a"
            }
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'arm64-v8a'

        }
    }

    buildTypes {
       release {
           minifyEnabled true
           proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
           signingConfig signingConfigs.release
       }
       debug {
           minifyEnabled false
           debuggable true
           jniDebuggable true
           renderscriptDebuggable true
           externalNativeBuild {
               cmake {
                   cppFlags "-DDEBUG=1"
               }
           }
       }
    }

    flavorDimensions "version"
    productFlavors {
        headless {
            dimension "version"
            applicationId "com.facebook.arvr.quillplayer"
            versionNameSuffix " : vr_only"
            buildConfigField "String", "BUILD_VARIANT", "\"headless\""
        }
        gallery {
            dimension "version"
            versionNameSuffix " : vrdesktop"
            buildConfigField "String", "BUILD_VARIANT", "\"gallery\""
            externalNativeBuild {
                cmake {
                    cppFlags "-DMEASURE_GPU_TIME=1"
                }
            }
        }
        retail_demo {
            dimension "version"
            versionNameSuffix " : vr_only"
            buildConfigField "String", "BUILD_VARIANT", "\"retail_demo\""
            externalNativeBuild {
                cmake {
                    cppFlags "-DRETAIL_DEMO=1"
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

dependencies {
    implementation fileTree(dir: '../../../../../../../../fbandroid/third-party/java/okhttp', include: ['okhttp-3.14.9.jar'])
    implementation fileTree(dir: '../../../../../../../../fbandroid/third-party/java/okio', include: ['okio-1.17.4.jar'])
    implementation fileTree(dir: '../../../../../../../../arvr/first-party/sdks/internal/panelappsdk', include: ['*.aar'])
    implementation fileTree(dir: '../facebook-breakpad', include: ['*.aar'])
    implementation fileTree(dir: '../oculus-crash-reporter', include: ['*.aar'])
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version" // or "kotlin-stdlib-jdk8"
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1")
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
}
repositories {
    mavenCentral()
    jcenter()
}
